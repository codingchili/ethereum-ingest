plugins {
    id "java"
}

project.version = "1.2.0"
project.group = 'com.codingchili.ethereumingest'

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

// https://stackoverflow.com/a/52615808
def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform = project.platform

if (!platform) {
    if (currentOS.isWindows()) {
        platform = 'win'
    } else if (currentOS.isLinux()) {
        platform = 'linux'
    } else if (currentOS.isMacOsX()) {
        platform = 'mac'
    }
}

dependencies {
    compile 'com.github.codingchili.chili-core:elastic:1.1.3'
    compile 'com.github.codingchili.chili-core:core:1.1.3'
    compile "org.openjfx:javafx-controls:11.0.2:${platform}"
    compile "org.openjfx:javafx-fxml:11.0.2:${platform}"
    compile "org.openjfx:javafx-graphics:11.0.2:${platform}"
    compile "org.openjfx:javafx-base:11.0.2:${platform}"
    compile 'org.web3j:core:4.2.0'
}

test {
    testLogging {
        exceptionFormat "full"
    }
    reports.html.enabled = false
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

jar {
    archivesBaseName = "ethereum-ingest-${platform}"
    zip64 true
    from {
        (configurations.compile).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Implementation-Title': 'ethereum-ingest',
                'Implementation-Version': version,
                'Main-Class': 'com.codingchili.ethereumingest.Service'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.DSA', 'META-INF/*.SF'
}

task alljavadoc(type: Javadoc) {
    source subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

task archiveJavadoc(type: Zip, dependsOn: alljavadoc) {
    baseName = 'javadocs'
    from fileTree(file("${buildDir}/docs/javadoc"))
}

task testReport(type: TestReport, dependsOn: 'build') {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}

task archiveTestReport(type: Zip, dependsOn: testReport) {
    baseName = 'testreport'
    from fileTree(file("$buildDir/reports/allTests"))
}
